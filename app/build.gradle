apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply from: 'https://raw.githubusercontent.com/JakeWharton/SdkSearch/master/gradle/projectDependencyGraph.gradle'

android {

    defaultConfig {
        applicationId "com.thirdwavelist.coficiando"

        versionCode 5
        versionName "1.1.0"

        proguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("src/release/release-keystore.jks")
            storePassword System.getenv("COFICIANDO_KEYSTOREPWD")
            keyAlias System.getenv("COFICIANDO_KEYALIAS")
            keyPassword System.getenv("COFICIANDO_KEYPWD")
        }

        debug {
            storeFile file("src/debug/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all"
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat "full"
            events "started", "skipped", "passed", "failed"
            showStandardStreams true
        }
    }

    lintOptions {
        checkReleaseBuilds true
        quiet false
        abortOnError true
        ignoreWarnings false
        xmlReport true
    }

    buildFeatures.dataBinding true

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude 'META-INF/**'
    }
}

dependencies {
    implementation project(":libraries:core-utils")
    implementation project(":libraries:core")
    implementation project(":libraries:navigation")
    implementation project(":features:home")
    implementation project(":features:details")

    implementation platform("com.google.firebase:firebase-bom:${Versions.firebase_bom}")

    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-config-ktx"

    implementation "androidx.appcompat:appcompat:${Versions.appcompat}"
    implementation "com.google.android.material:material:${Versions.material_design}"
    implementation "androidx.constraintlayout:constraintlayout:${Versions.constraint_layout}"

    implementation "com.google.code.gson:gson:${Versions.gson}"

    implementation "com.google.dagger:hilt-android:${Versions.hilt}"
    kapt "com.google.dagger:hilt-android-compiler:${Versions.hilt}"

    implementation "androidx.navigation:navigation-ui-ktx:${Versions.nav_components}"
    implementation "androidx.navigation:navigation-fragment-ktx:${Versions.nav_components}"

    implementation "com.airbnb.android:lottie:${Versions.lottie}"

    implementation "com.squareup.okhttp3:okhttp:${Versions.okhttp}"
}

apply plugin: 'com.google.gms.google-services'